\ Forth kernel for 6502 on C64.
\ These are HOST vocab words that define assembler words on Target - their
\ mirror words are regular.

\ A code definition is the (TCREATE) header, then then code.
: code      ( "name' -- ) (TCREATE) ;
: end-code  ( -- )        next, ;
\ End marker, but no NEXT.
: end-code-bare ( -- ) ;


\ Arithmetic =================================================================
: binary, ( pre-xt op-xt )
    >R
    ( pre-xt ) execute
    2 zp,x lda,
    0 zp,x R@ execute
    2 zp,x sta,
    3 zp,x lda,
    1 zp,x R> execute
    3 zp,x sta,
    inx, inx, ;

: noop ( -- ) ;

code +    ' clc, ' adc, binary, end-code
code -    ' sec, ' sbc, binary, end-code
code AND  ' noop ' and, binary, end-code
code OR   ' noop ' ora, binary, end-code
code XOR  ' noop ' eor, binary, end-code

code 2*  label 2*-inner  0 zp,x asl,   1 zp,x rol,   end-code
code 2U/                 1 zp,x lsr,   0 zp,x ror,   end-code
code 2/
  clc,
  1 zp,x lda, \ N is now set accordingly.
  mi? if,   sec,   then,
  1 zp,x ror,
  0 zp,x ror,
end-code

code LO
  0 #    lda,
  1 zp,x sta,
end-code

code HI
  1 zp,x lda,
  0 zp,x sta,
  0 #    lda,
  1 zp,x sta,
end-code

code INVERT
    $ff # lda,   0 zp,x eor,   0 zp,x sta,
    $ff # lda,   1 zp,x eor,   1 zp,x sta,
end-code


\ Multiplication =============================================================
\ Taken from _Assembly in One Step_'s example 5-6 - 16x16 -> 32-bit multiply.
\ The operands are taken from the stack, and the product in W_lo .. W2_hi.
\ Call the 4 product bytes, low to high, p0 to p3.
code UM* ( u u -- ud )
  0   #  lda,        \ Clear the upper two bytes of the result.
  w2l zp sta,
  w2h zp sta,
  16  #  ldy,        \ Multiplier bit count of 16.

  label UM*-next-bit
    1 zp,x lsr,      \ Shift two-byte multiplier (TOS) right
    0 zp,x ror,
    cs? if,          \ If we just shifted out a 1, add to the result.
      w2l zp lda,    \ Fetch p2
      clc,
      2   zp,x adc,  \ Add NOS_lo to it
      w2l zp   sta,  \ And store it back
      w2h zp   lda,
      3   zp,x adc,  \ Then NOS_hi + p3, but DON'T write it back.
    then,
    acc ror,         \ Rotate the four-byte product right.
    w2h zp   sta,    \ Write p3
    w2l zp   ror,
    wh  zp   ror,
    wl  zp   ror,
    dey,             \ Decrement the bit count.
    UM*-next-bit bne,

  \ End of loop - product is in W (low) and W2 (high).
  \ Double-cell values are stored ( lo hi ), so overwrite NOS and TOS.
  wh  zp lda,   3 zp,x sta,
  wl  zp lda,   2 zp,x sta,
  w2h zp lda,   1 zp,x sta,
  w2l zp lda,   0 zp,x sta,
end-code


\ Comparisons ================================================================
code =
  0 # ldy,
  1 zp,x lda,
  3 zp,x cmp,
  eq? if,
    0 zp,x lda,
    2 zp,x cmp,
    eq? if,
      $ff # ldy,
    then,
  then,
  2 zp,x sty,
  3 zp,x sty,
  inx, inx,
end-code

code U<  ( lhs rhs -- ? ) \ NOS - TOS - C=0 iff NOS < TOS as desired.
  0 #    ldy,  \ Flag
  2 zp,x lda,
  0 zp,x cmp,
  3 zp,x lda,
  1 zp,x sbc,  \ C=0 iff NOS < TOS, so if carry is clear, LDY $ff.
  cc? if,
    $ff # ldy,
  then,
  2 zp,x sty,
  3 zp,x sty,
  inx, inx,
end-code

\ TODO: Some kind of conditional compilation of target words - these are
\ overwritten in shared/basics.ft currently.
code 0= ( x -- ? )
  $ff #    ldy,
  1   zp,x lda,
  0   zp,x ora,
  ne? if,
    0 # ldy,
  then,
  0   zp,x sty,
  1   zp,x sty,
end-code

code 0< ( x -- ? )
  $ff #    lda,
  1   zp,x ldy,
  pl? if,
    0 # lda,
  then,
  0   zp,x sta,
  1   zp,x sta,
end-code

code <  ( lhs rhs -- ? ) \ Subtracting TOS from NOS.
  2 zp,x lda,
  0 zp,x cmp,
  3 zp,x lda,
  1 zp,x sbc,
  vs? if,
    $80 # eor,
  then,
  \ Top bit of A is now 1 if <, 0 if >=. Convert that to a Forth flag.
  $80 # and,   \ Check that top bit - if it's 0 A is now 0.
  ne? if,
    $ff # lda, \ And if it wasn't 0, A is now $ff
  then,
  2 zp,x sta,
  3 zp,x sta,  \ Store A to both parts of NOS.
  inx, inx,    \ Pop one stack cell.
end-code


\ Stack operations ===========================================================
code DUP
label DUP-code
    dex, dex,
    2 0 stack-cp,
end-code

code DROP inx, inx, end-code

code OVER
    dex, dex,
    4 0 stack-cp,
end-code

code SWAP
    2 0 w-swap,
end-code

code ROT ( 45 23 01 -- 23 01 45 )
    4 zp,x ldy,
    2 zp,x lda,
    4 zp,x sta,
    0 zp,x lda,
    2 zp,x sta,
    0 zp,x sty,

    5 zp,x ldy,
    3 zp,x lda,
    5 zp,x sta,
    1 zp,x lda,
    3 zp,x sta,
    1 zp,x sty,
end-code

code -ROT ( 45 23 01 -- 01 45 23 )
    0 zp,x ldy,
    2 zp,x lda,
    0 zp,x sta,
    4 zp,x lda,
    2 zp,x sta,
    4 zp,x sty,

    1 zp,x ldy,
    3 zp,x lda,
    1 zp,x sta,
    5 zp,x lda,
    3 zp,x sta,
    5 zp,x sty,
end-code

code ?DUP
    0 zp,x lda,
    1 zp,x ora,
    ne? if, \ If the combo is not 0, jump to DUP
        DUP-code $ jmp,
    then,
end-code

code NIP ( y x -- x ) 0 2 stack-cp, inx, inx, end-code

code TUCK ( y x -- x y x )
    dex, dex,
    2 0 stack-cp,
    4 2 stack-cp,
    0 4 stack-cp,
end-code

code 2DUP ( y x -- y x y x )
    dex, dex, dex, dex,
    4 0 stack-cp,
    6 2 stack-cp,
end-code

code 2DROP ( z y x w -- )
  inx, inx, inx, inx,
end-code

code 2SWAP ( z y x w -- x w z y )
    0 4 w-swap,
    2 6 w-swap,
end-code

code 2OVER ( z y x w -- z y x w z y )
    dex, dex, dex, dex,
    10 2 stack-cp,
     8 0 stack-cp,
end-code

code SP@ ( -- psp )
    txa,       \ Get the X before we move the stack pointer.
    dex, dex,
    0 zp,x sta,
    \ High byte is always 0
    0 #    lda,
    1 zp,x sta,
end-code

code SP! ( psp -- )
    0 zp,x lda,
    tax,
end-code

\ The return stack will have the address after the RP@ call on it.
\ RP! will want to return from RP! to the caller of RP@ - so that works.
code RP@
    wl zp stx,  \ Preserve PSP
    tsx,        \ Get the hardware stack pointer.
    txa, tay,   \ And put it in Y.
    wl zp ldx,  \ Put PSP back
    dex, dex,   \ Make space
    0 zp,x sty, \ Low byte is S (from Y)
    1 #    lda, \ High byte is always 1.
    1 zp,x sta,
end-code

code RP! \ Need to retrieve the return addres, fix it, and jump.
    clc,
    pla,
    1 # adc,
    wl zp sta,
    pla,
    0 # adc,
    wh zp sta,  \ Return address in W

    0 zp,x lda, \ Low byte - the new S.
    inx, inx,   \ Drop the value off the stack.
    w2l zp stx, \ Set PSP aside.
    tax,        \ Put the new S in X.
    txs,        \ Then S
    w2l zp ldx, \ And put PSP back.
    wl $ (jmp), \ Jump indirect out of here.
end-code-bare


\ TODO: >R and R> are a bit slow implemented this way; is there a better way?
code >R R>W, >R, W>R, end-code
code R> R>W, R>, W>R, end-code
code R@
    dex, dex,
    rsp>Y,    \ System stack is empty-descending. NORS is at $103,Y and 104,Y.
    $104 $,Y  lda,
    1    zp,x sta,
    $103 $,Y  lda,
    0    zp,x sta,
end-code

\ Depth is sp0 - X, shifted right by 1.
code DEPTH
    wl zp stx,  \ Put the current X into memory.
    sp0 # lda,  \ top in A
    sec,
    wl zp sbc,  \ top-X in A now.
    dex, dex,
    acc lsr,        \ (top-X)/2 in A
    0 zp,x sta, \ That's the low byte.
    0 #    lda,
    1 zp,x sta, \ High byte is always 0.
end-code


\ Memory =====================================================================
code @
    wl tos>ZP,     \ Set aside the address.
    0 # ldy,       \ Offset of 0 in Y
    wl (zp),y lda, \ Lo byte -> A
    0  zp,x   sta, \ Into TOS
    iny,
    wl (zp),y lda, \ Hi byte -> A
    1  zp,x   sta, \ Into TOS
end-code

code ! ( value addr -- )
    wl tos>ZP,     \ Set aside the address.
    0 # ldy,       \ 0 -> Y
    2  zp,x   lda, \ NOS_lo -> A
    wl (zp),y sta, \ Lo byte -> mem
    iny,           \ 1 -> Y
    3  zp,x   lda, \ NOS_hi -> A
    wl (zp),y sta, \ Hi byte -> mem
    inx, inx, inx, inx,
end-code

code C@ ( c-addr -- b )
    0 (zp,x) lda,
    0 zp,x   sta,
    0 #      lda,
    1 zp,x   sta, \ High byte is always 0
end-code

code C! ( b c-addr -- )
    2 zp,x   lda,
    0 (zp,x) sta,
    inx, inx, inx, inx,
end-code

\ code 2@ ( a-addr -- [a-addr+2] [a-addr] )
\   wl TOS>ZP,
\   dex, dex,
\   wl (zp)>tos,   \ [a-addr] -> TOS
\   2  #      ldy, \ TODO: Maybe bad (zp),y
\   wl (zp),y lda,
\   2  zp,x   sta,
\   iny,
\   wl (zp),y lda,
\   3  zp,x   sta,
\ end-code
\
\ code 2! ( [addr+2] [addr] addr -- )
\   wl tos>zp,
\   inx, inx,
\   wl tos>(zp),
\   inx, inx,
\   wl tos>(zp),
\ end-code

code +! ( delta addr -- )
    wl tos>ZP,
    0  #      ldy, \ 0 -> Y
    clc,
    2  zp,x   lda, \ delta_lo -> A
    wl (zp),y adc, \ mem_lo + delta_lo -> A
    wl (zp),y sta, \ result_lo -> mem_lo
    iny,
    3  zp,x   lda, \ delta_hi -> A
    wl (zp),y adc, \ mem_hi + delta_hi -> A
    wl (zp),y sta, \ result_hi -> mem_hi
    inx, inx, inx, inx,
end-code

code EXECUTE ( xt -- )
    \ Pop the xt into W, then JMP-indirect through it.
    wl tos>ZP,
    inx, inx,
    wl $ (jmp), \ That will return to the caller of EXECUTE, as intended.
end-code-bare

\ TODO: BREAK? Is there any way to trigger a breakpoint on C64?


\ Variables and core states ==================================================
label var_latest      0 asm,16
label var_base        0 asm,16
label var_state       0 asm,16
label var_handler     0 asm,16

label var_last_parse  0 asm,16 0 asm,16
label var_source      0 asm,16 0 asm,16
label var_in_ptr      0 asm,16
label var_source_id   0 asm,16
label input-buf       128 tcode-allot

\ The dictionary pointer is in the zero page for speed.
code DP herel lit, end-code

label var_dp_ptr 0 asm,16

code , ( x -- )
    0     zp,x   lda,
    0     #      ldy,
    herel (zp),y sta,
    1     zp,x   lda,
    iny,
    herel (zp),y sta,

    2 herel ZP+,
    inx, inx,
end-code

code C, ( b -- )
    0     zp,x   lda,
    0     #      ldy,
    herel (zp),y sta,
    1 herel ZP+,
    inx, inx,
end-code

\ Compilation means compiling a JSR byte first, then jumping into , above.
\ OPT: This could probably be optimized for space at the cost of speed.
code COMPILE, ( xt -- )
    \ Write the opcode for JSR $1234: $20.
    0     #      ldy,
    $20   #      lda,
    herel (zp),y sta, \ $20 -> (dp)
    iny,
    0     zp,x   lda,
    herel (zp),y sta, \ xt_lo -> (dp+1)
    iny,
    1     zp,x   lda,
    herel (zp),y sta, \ xt_hi -> (dp+2)

    3 herel ZP+,
    inx, inx,
end-code


code HANDLER      var_handler    lit, end-code
code 'LATEST      var_latest     lit, end-code
code STATE        var_state      lit, end-code
code BASE         var_base       lit, end-code
code 'SOURCE      var_source     lit, end-code
code 'SOURCE-ID   var_source_id  lit, end-code
code >IN          var_in_ptr     lit, end-code
code TIB          input-buf      lit, end-code
code LAST-PARSE   var_last_parse lit, end-code
code F_IMMED      f_immed        lit, end-code

\ TIB 0c42 'SOURCE 0c3a
\ >IN 0c3e

\ More arithmetic ============================================================
code 1+
  0 zp,x inc,
  eq? if,
    1 zp,x inc,
  then,
end-code

code 1-
  \ The eq? trick used for 1+ carrying doesn't work for 1-; so use actual SBCs.
  sec,
  0 zp,x lda,
  1 #    sbc,
  0 zp,x sta,
  1 zp,x lda,
  0 #    sbc,
  1 zp,x sta,
end-code

code CELL   2 lit,                    end-code
code CELLS  2*-inner $ jmp,           end-code-bare
code CHARS                            end-code
code CELL+  2 TOS+lit,                end-code
code CHAR+  1 TOS+lit,                end-code

code PAGES
  0 zp,x lda,
  1 zp,x sta,
  0 #    lda,
  0 zp,x sta,
end-code

code BYTES  ( x -- x_lo x_hi )
  dex, dex,
  3 zp,x lda,   0 zp,x sta,   \ Put x_hi into the new bottom slot.
  0 #    lda,
  3 zp,x sta,   1 zp,x sta,   \ And 0 into the two high bytes.
end-code

\ Double-cell arithmetic =====================================================
\ Minimal set - the words used in shared/: S>D D+ UDM*
\ Note that we already have UM* ( u u -- ud )
\ TODO: Implement the rest.

code S>D
  dex, dex,
  0 #    lda,
  3 zp,x ldy, \ Sets N based on the top bit.
  mi? if,   $ff # lda,   then,
  0 zp,x sta,
  1 zp,x sta,
end-code

: ripple-add, ( zpA zpB -- )
  ( zpB) zp,x lda,
  dup    zp,x adc,
  ( zpA) zp,x sta, ;

code D+ ( l1 h1 l2 h2 -- l3 h3 )
  \ Two 4-byte values to be added all together. Results written over NOS.
  clc,
  4 0 ripple-add,
  5 1 ripple-add,
  6 2 ripple-add,
  7 3 ripple-add,
  inx, inx, inx, inx,
end-code


code UM/MOD ( ulo uhi udiv -- ur uq )
  \ Dividing a 32-bit unsigned dividend by an unsigned 16-bit divisor to get
  \ an unsigned 16-bit quotient and unsigned 16-bit remainder.
  \ This works by successive bit-shifting until we run out of divisor bits.
  \ The dividend goes in W (lo) and W2 (hi); then W2 holds the remainder and
  \ W the quotient.
  \ Each steps shifts this pair left, moving the top bit of the low/quotient
  \ into the high/remainder.
  \ We shift in a 0 there, but if the remainder is >= the divisor, we subtract
  \ the divisor from the remainder and make bit 0 of the quotient 1.
  \ After cycling for as many bits as the divisor is wide (16), all the dividend
  \ bits have been shifted out of the quotient, and the remainder holds exactly
  \ the remainder we want!
  \ W2 = high dividend/remainder; W = low dividend/quotient   (r0 and r1)
  4 zp,x lda,   wl  zp sta,
  5 zp,x lda,   wh  zp sta,  \ ulo -> W
  2 zp,x lda,   w2l zp sta,
  3 zp,x lda,   w2h zp sta,  \ uhi -> W2
  16 # ldy,                  \ bit count -> Y

  0 zp,x lda,
  1 zp,x ora,
  eq? if,
    inx, inx,
  then,

  label UM/MOD-loop
    \ Shift W2:W left as a 32-bit value.
    wl  zp asl,
    wh  zp rol,
    w2l zp rol,
    w2h zp rol,

    \ Check remainder U< divisor by subtracting remainder - divisor.
    \ If C is set (no borrow) then we have remainder U>= divisor and must
    \ drain the remainder and put a 1 into the quotient.
    sec,
    w2l zp   lda,
    0   zp,x sbc, \ SBC rather than CMP, we want the value too.
    w3l zp   sta, \ Save the low byte in W3_lo
    w2h zp   lda,
    1   zp,x sbc,
    cs? if,
      \ We've already got remainder - divisor! hi byte in A, lo in W3_lo
      \ Store that properly in W2, then set low bit of the quotient (W).
      w2h zp   sta,
      w3l zp   lda,
      w2l zp   sta,
      wl  zp   inc, \ It was just shifted left, so the low bit is always 0.
    then,

    dey,
  UM/MOD-loop bne, \ Keep looping if it's not 0 yet.

  \ Now W2 is the remainder, W the quotient.
  inx, inx,       \ Pop the divisor; there's too many values on the stack.
  wl  zp   lda,   0 zp,x sta,
  wh  zp   lda,   1 zp,x sta,
  w2l zp   lda,   2 zp,x sta,
  w2h zp   lda,   3 zp,x sta,
end-code


\ MOVEs ======================================================================

\ Helper that pops the stack and returns to the caller's caller if Z is set.
\ Saving some space at the cost of a few cycles.
\ Pops *3* stack cells, since this is called from MOVE< MOVE> and FILL.
label (bail-if-empty)
  0 zp,x lda,
  1 zp,x ora,
label (bail-if-0)
  eq? if,
    txa,
    clc,
    6 # adc,
    tax,
    pla, pla, \ Destroy one return address (inside MOVE< etc.)
  then,
  rts,

code MOVE< ( src dst u -- ) \ Copies from low addresses to high.
  \ This uses (zp),Y addressing to copy bytes relatively painlessly.
  \ It can use 8-bit INYs most of the time, and use the Z flag to determine
  \ if carrying if necessary.
  \ First check if the length is all 0.
  (bail-if-empty) $ jsr,

  wl  tos>ZP,   \ len -> W
  inx, inx,
  w3l tos>ZP,   \ dst -> W3
  inx, inx,
  w2l tos>ZP,   \ src -> W2
  inx, inx,

  txa, pha,     \ Push PSP (X).

  \ Len in X counting down; memory offset in Y
  0  #  ldy,
  wl zp ldx,

  label MOVE<-loop
    w2l (zp),Y  lda,    \ 5c
    w3l (zp),Y  sta,    \ 6c
    iny,                \ 2c
    eq? if, \ If that just rolled over to 0, increment the pointers.  (BNE: 3c x255 + 2c x1)
      w2h zp    inc,    \ 5c \
      w3h zp    inc,    \ 5c /\ these only run every 256 cycles.
    then,

    dex,    \ Then decrement the length to go.    2c

    \ Fast case: if that DEX left it nonzero, continue.
    MOVE<-loop bne,                             \ 3c x 255 + 2c x 1

    \ Slow case: decrement the high byte of the length.
    \ Note that X is already 0 in this case, ready to continue.
    wh zp lda,
    ne? if, \ If the top byte of the length isn't 0, decrement and continue.
      wh zp dec,
      MOVE<-loop $ jmp,
    then,
    \ Or we're done; so pull the old X and return.
    pla, tax, rts,


code MOVE> ( src dst u -- ) \ Copies from high addresses to low.
  \ This uses (zp),Y addressing to copy bytes relatively painlessly.
  \ It can use 8-bit INYs most of the time, and use the Z flag to determine
  \ if carrying is necessary.
  \ Note that due to the nature of the arithmetic that MOVE< is faster and
  \ should be preferred if memory regions don't overlap.

  \ First check if the length is all 0.
  (bail-if-empty) $ jsr,

  wl  tos>ZP,   \ len -> W
  inx, inx,

  0   zp,x lda,
  w3l zp   sta,
  1   zp,x lda,
  clc,
  wh  zp   adc, \ Add the high byte of the length to the src and dst.
  w3h zp   sta, \ dst -> W3
  inx, inx,

  0   zp,x lda,
  w2l zp   sta,
  1   zp,x lda,
  clc,
  wh  zp   adc, \ Add the high byte of the length to the src and dst.
  w2h zp   sta, \ src -> W3
  inx, inx,

  \ Len slash memory offset in Y.
  wl  zp ldy,

  label MOVE>-loop
    \ Start with DEY, because it's pointing past the end.
    dey,
    w2l (zp),Y  lda,
    w3l (zp),Y  sta,
    tya,
    MOVE>-loop bne,
    \ If Y is 0 and about to go to $FF, decrement the pointers.
    w2h zp dec,
    w3h zp dec,

    \ If the high byte of the length is 0, return.
    wh  zp lda,
    eq? if, rts, then,

    \ If not, decrement it and continue.
    wh  zp dec,
    MOVE>-loop $ jmp,


code FILL ( c-addr u char -- )
  \ Bail if the length is 0. Can't use bail-if-empty because of different
  \ stacking order.
  2 zp,x lda, 3 zp,x ora,
  (bail-if-0) $ jsr,

  wl  tos>zp,   \ char -> W
  inx, inx,
  w2l  tos>zp,  \ len  -> W2
  inx, inx,
  w3l  tos>zp,  \ addr -> W3
  inx, inx,
  txa, pha,     \ Save X to hardware stack.

  0   #  ldy, \ Memory offset Y starts at 0.
  w2l zp ldx, \ len_lo -> X
  label FILL-loop
    eq? if, \ Low byte of length in X is 0.
      w2h zp lda, \ len_hi -> A
      eq? if,     \ If *that* is 0, we're done.
        pla, tax, rts,
      then,
      \ If we're still here, decrement the high byte and continue.
      \ X is still 0, which is fine.
      w2h zp dec,
    then,

    wl  zp     lda, \ ch -> A
    w3l (zp),y sta, \ A  -> mem

    iny,
    eq? if, \ If Y just rolled over to 0, increment addr_hi
      w3h zp inc,
    then,

    dex,
  FILL-loop $ jmp,
end-code-bare   \ It jumps out of the middle when done.


\ DO LOOP machinery ==========================================================
\ DO LOOPs can be built right in as loops inside the containing word, using
\ the (return) stack for nesting and a ZP cell for the current index.
\ The setup and tear-down machinery (DO) and (LOOP-END) are too lengthy to
\ inline, however.
code (DO) \ ( limit index --     R: ret -- old_index limit ret )
  R>W,                       \ Get return address out of the way.
  indexh zp   lda,   pha,    \ Push old index_hi
  indexl zp   lda,   pha,    \ Push old index_lo
  3      zp,x lda,   pha,    \ Push limit_hi
  2      zp,x lda,   pha,    \ Push limit_lo
  indexl tos>zp,             \ New limit in index register.
  inx, inx, inx, inx,        \ Drain stack
  W>R,                       \ Put the return address back.
end-code

code (LOOP-END) ( delta -- done?     R: old-index limit ret -- ditto )
  rsp>Y,            \ Juggle the RSP into Y
  \ Put 0 into W3_lo and _hi for later use.
  0   #  lda,
  w3l zp sta,
  w3h zp sta,

  \ We need index - limit first, in W.
  sec,
  indexl zp  lda,
  $0103  $,y sbc,
  wl     zp  sta,   \ index - limit LO -> W_lo
  indexh zp  lda,
  $0104  $,y sbc,
  wh     zp  sta,   \ index - limit HI -> W_hi

  \ Add it to the delta from TOS into W2 (we still need the delta).
  clc,
  0   zp,x lda,
  wl  zp   adc,
  w2l zp   sta,
  1   zp,x lda,
  wh  zp   adc,
  w2h zp   sta,     \ delta + index - limit -> W2

  \ XOR the high bytes of d+i-l and i-l
  \ d+i-l_hi is still in A.
  wh zp eor,
  pl? if, \ If N is clear, ie. same flag, DEC W3_lo to make it $ff, the first flag.
    w3l zp dec,
  then,

  \ Then calculate delta_hi XOR i-l_hi
  \ i-l is in W; delta in TOS.
  wh zp   lda,
  1  zp,x eor,
  pl? if,      \ N clear = same sign, store $ff into W3_hi, second flag.
    w3h zp dec,
  then,

  w3h zp lda,  \ Second flag   -> A
  w3l zp ora,  \ OR first flag -> A
  $ff #  eor,  \ Negate it - this is the done flag.
  w3l zp sta,  \ Done flag -> W3_lo for a sec

  clc,
  indexl zp   lda,
  0      zp,x adc,
  indexl zp   sta,
  indexh zp   lda,
  1      zp,x adc,
  indexh zp   sta,   \ index + delta -> index

  \ Get the done flag from W3_lo to both bytes of TOS.
  w3l zp   lda,
  0   zp,x sta,
  1   zp,x sta,
end-code


code UNLOOP ( -- R: old-index limit ret -- ret )
  R>W,
  pla, pla,
  pla,   indexl zp sta,
  pla,   indexh zp sta,
  W>R,
end-code

code I ( -- index1 )
  dex, dex,
  indexl zp   lda,
  0      zp,x sta,
  indexh zp   lda,
  1      zp,x sta,
end-code

code J ( -- old-index   R: old-index limit ret -- old-index limit ret )
  dex, dex,
  rsp>Y,              \ Juggle the RSP into Y
  \ NB: The hardware stack is empty-descending, so RTOS is at $101 $,Y and $102 $,Y.
  $105 $,y  lda,      \ old-index_lo -> A
  0    zp,x sta,      \ -> TOS_lo
  $106 $,y  lda,      \ old-index_hi -> A
  1    zp,x sta,      \ -> TOS_hi
end-code


\ Keyboard ===================================================================
label forth-key-semaphore 0 asm,
label forth-key           0 asm,

code KEY ( -- key )
  1 # lda,
  forth-key-semaphore $ sta,
  label (KEY)-loop
    forth-key-semaphore $ lda, \ Spin waiting for the interrupts.
  (KEY)-loop bne,
  \ Now the key is populated.
  dex, dex,
  0         # lda,   1 zp,x sta,
  forth-key $ lda,   0 zp,x sta,
end-code


\ Main =======================================================================
REQUIRE interrupts.ft

\ Some KERNAL routines for startup.
$fda3 CONSTANT k-ioinit
$fd50 CONSTANT k-ramtas
$fd15 CONSTANT k-restor
$ff5b CONSTANT k-cint

$0314 CONSTANT irq-indirect

label debug-spin
  0 # lda,
  debug-spin beq,

\ Machine code entry point.
\ Sets up the basic system - bank switching, interrupt vectors, etc.
label main
  sei, \ No interrupts for now.
  cld, \ Just in case, no one wants decimal mode.
  $ff # ldx,
  txs,
  sp0 # ldx,

  \ TODO: See about smaller/faster setup, and maybe copying the Forth system
  \ to a lower address to make maximum contiguous memory for the dictionary.

  \ For now I'm using the kernal ROM startup routines.
  k-ioinit $ jsr,
  k-ramtas $ jsr, \ TODO: Most likely to be skippable - just set PAL/NTSC?
  k-restor $ jsr,
  k-cint   $ jsr,

  vec-irq irq-indirect w!,

  cli,  \ Interrupts are rigged up again.

  \ From this point we no longer need the BASIC or KERNAL ROMs except for IRQ.
  \ TODO: Crib the minimal interrupt-handling code from the KERNAL ROM into the
  \ Forth implementation and discard the KERNAL.

  \ TODO: Adjust the memory banks at this point, if we're going to.

  \ Set the initial DP into the ZP slot.
  var_dp_ptr    $  lda,
  herel         zp sta,
  var_dp_ptr 1+ $  lda,
  hereh         zp sta,

  \ The hardware stack starts configured - set up the data stack by setting X.
  sp0 # ldx,

  \ Write 7 into $0000 (PLA control) so the low bits of $0001 are outputs.
  7 0 b!,

  \ There's no fancy FORTH initialization, IP, or NEXT with STC.
  \ Jump through the entry-point cell, which contains an xt, ie. a code address.
  entry-point $ (jmp),

\ Write a JMP to main at the start of the machine code.
\ (There's a dummy jump there to be overwritten.
code-there @
machine-code-entry code-there !
main $ jmp,
code-there !
