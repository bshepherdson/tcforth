\ Disk reading routines for the DCPU family's M35fd
\ Read only, reading into 16 512-word buffers from $6000 to $8000.
\ These are allocated in a round-robin fashion.
target definitions

$6000 CONSTANT disk-buffers
16    CONSTANT #buffers
$200  CONSTANT /buffer

CREATE buffer-table #buffers cells allot
VARIABLE next-buffer

: find-buffer ( target -- buf-addr|0 )
  #buffers 0 DO
    dup   buffer-table i cells + @ =
    IF drop   i /buffer * disk-buffers + UNLOOP EXIT THEN
  LOOP drop false ;

\ Round-robin selects the next disk buffer to evict and replace.
: free-buffer ( -- buf-index )
  next-buffer @
  dup 1+ #buffers mod next-buffer ! ;

: disk>buffer ( blk# dest-addr -- )
  2 -rot $19   ( 2 blk# dest-addr ) dev-m35fd @ >device   await-disk ;

\ Loads a particular buffer (by its disk block index) into memory.
\ If it's already loaded, just returns it.
\ Otherwise evicts something and reads the block in.
: load-buffer ( blk# -- buf-addr )
  dup find-buffer ?dup IF nip EXIT THEN ( blk# )
  free-buffer      ( blk# buf-index )
  2dup cells buffer-table + ! ( blk# buf-index )
  /buffer *
  tuck disk>buffer ( buf-addr ) ;


