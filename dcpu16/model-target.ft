\ Target words to get metacompiled, that hide the details of the machine and the
\ Forth model (eg. ITC vs. DTC vs. STC)
\ This file: DTC on the DCPU-16.

\ ,DOCOL is already defined.
target definitions

\ A code field here is jsr lit, $7c20 + the literal address.
target : !CF ( codeword-addr xt -- ) $7c20 over !   cell+ ! ;
target : ,CF ( codeword-addr -- ) here 2 cells allot   !cf ;

target : ,DOCOL ( -- ) [ host jsr_docol tcode@ tliteral target ] , ;

target : PAD ( -- addr ) $fb00 ;

host REQUIRE ../shared/model-target.ft

\ DOES> ======================================================================
\ Implementing sequence 2
\ (DOES>) runs in eg. CONSTANT to properly compile a particular constant.
\ Inside target (DOES>), TORS points at the EXIT. Just after it is the
\ dodoes-code that should become the new codeword's jump location.
host definitions

\ Compiles code that will itself compile an exit.
\ Runs while assembling ; for example.
: EXIT-compile, ( -- )
  'EXIT @ tliteral   [T'] compile, tcompile, ;

\ Runs during compilation of TARGET : DOES> - compile code into its thread!
: TDOES,
  [T'] (DOES>)        tliteral   [T'] compile, tcompile,
  EXIT-compile,
  pfa_tos_code_top pfa_tos_code DO
    i tcode@ tliteral   [T'] , tcompile,   1 tcells +LOOP
  jsr_docol    tcode@ tliteral   [T'] ,        tcompile, ;
