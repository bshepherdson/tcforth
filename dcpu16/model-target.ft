\ Target words to get metacompiled, that hide the details of the machine and the
\ Forth model (eg. ITC vs. DTC vs. STC)
\ This file: DTC on the DCPU-16.

\ ,DOCOL is already defined.
target definitions

\ Add ,CF, which expects the address to jump to on the stack.
\ A code field here is jsr lit, $7c20 + the literal.
target : ,CF ( codeword-addr -- ) $7c20 , , ;

target : ,DOCOL ( -- ) [ host jsr_docol tcode@ tliteral target ] , ;

host config data-indirect? @ [IF]
  target : ,PFA ( -- ) here cell+ , ;
host [ELSE]
  target : ,PFA ( -- ) ;
host [THEN]

target : ,xt-slot ( -- ) ,pfa ;

target : LITERAL ( C: x --    X: -- x ) [ host lit tliteral target ] compile, , ; IMMEDIATE
  host acts: tliteral ; IMPERATIVE

\ Compiles a string literal into a thread.
\ On ITC/DTC that means the address of DOSTRING, the length, then the words.
target : ,DOSTRING ( c-addr u -- )
  [ host dostring tliteral target ] compile,   \ Compile the codeword first.
  dup >R c,        \ Then the length
  here R@ move     \ And the text
  R> chars allot ; \ And make room for it.

target : ALIGNED ;
target : ALIGN ; host acts: ;

target : >CFA ( nt -- cfa )
  cell+ dup c@ [ host mask_len tliteral target ] and + char+ aligned
  [ host config names-indirect? @ [IF] target ] @ [ host [THEN] target ] ;

host config names-indirect? @ [IF]
target : /NAME-FIELD ( -- u ) 1 cells ;
host [ELSE]
target : /NAME-FIELD ( -- u ) 0 ;
host [THEN]

host config names-indirect? @ [IF]
target : >BODY ( xt -- addr ) 2 cells + @ ;
host [ELSE]
target : >BODY ( xt -- addr ) 2 cells + ;
host [THEN]

target : PAD ( -- addr ) $fb00 ;

\ DOES> ======================================================================
\ Implementing sequence 2
\ (DOES>) runs in eg. CONSTANT to properly compile a particular constant.
\ Inside target (DOES>), TORS points at the EXIT. Just after it is the
\ dodoes-code that should become the new codeword's jump location.
\ $7c20 is JSR lit on DCPU.
target : (DOES>) latest >cfa   $7c20 over !    cell+ R@ cell+ swap ! ;

host T' (DOES>) '(DOES>) !
host T' EXIT    'EXIT !

host definitions

\ Compiles code that will itself compile an exit.
\ Runs while assembling ; for example.
: EXIT-compile, ( -- )
  'EXIT @ tliteral   [T'] compile, tcompile, ;

: asm-lit ( -- x )
  -1 tcells tcode-allot
  code-space @   tcode@ ;

\ Runs during compilation of TARGET : DOES> - compile code into its thread!
: TDOES,
  [T'] (DOES>)        tliteral   [T'] compile, tcompile,
  [T'] EXIT           tliteral   [T'] compile, tcompile,
  pfa_tos_code_top pfa_tos_code DO
    i tcode@ tliteral   [T'] , tcompile,   1 tcells +LOOP
  \ pfa_tos_code tcode@ tliteral   [T'] ,        tcompile,
  jsr_docol    tcode@ tliteral   [T'] ,        tcompile, ;
