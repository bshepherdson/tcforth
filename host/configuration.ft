\ Defines several configuration parameters which control different parts of
\ the metacompiler, eg. whether code space overlaps data space, whether code
\ space is read-only, whether to include name space and input parsing code, etc.

REQUIRE vocabularies.ft
host definitions

begin-structure configuration
  field: has-dictionary?
  field: keyboard-input?
  field: data-space-blank?
  field: mix-code-and-data?
  field: mix-code-and-name?
  field: char-bits
  field: cell-width      \ In target address units
  field: instr-width
  field: little-endian?
end-structure

configuration allocate drop CONSTANT config

\ Derived value that's used in a few places.
: colocated-names? ( -- ? )
  config has-dictionary? @
  config mix-code-and-data? @ and
  config mix-code-and-name? @ and ;

\ A built-in configuration, with all three spaces unified, fully interactive.
: interactive-forth! ( -- )
  config has-dictionary?    ON
  config keyboard-input?    ON
  config data-space-blank?  OFF
  config mix-code-and-data? ON
  config mix-code-and-name? ON ;

\ A built-in configuration, with no interactivity or keyboard, all spaces
\ separate, and a blank data space.
: rom-application! ( -- )
  config has-dictionary?    OFF
  config keyboard-input?    OFF
  config data-space-blank?  ON
  config mix-code-and-data? OFF
  config mix-code-and-name? OFF ;

\ Add-on to other configurations.
: separate-spaces! ( -- )
  config mix-code-and-name? OFF
  config mix-code-and-data? OFF ;

\ Default configuration:
\ - interactive Forth
interactive-forth!


\ Helpers ====================================================================
\ Typically 8 or 16, the number of bits per target address unit.
: tcharbits ( -- u )     config char-bits @ ;
: tcells    ( u1 -- u2 ) config cell-width @ * ;
: tcellbits ( -- u )     tcharbits tcells ;
: tchars    ( u1 -- u2 ) ;
: tchar+    ( u1 -- u2 ) 1+ ;
: tcell+    ( u1 -- u2 ) 1 tcells + ;

: taligned  ( tc-addr -- t-addr ) 1 tcells 1- +   1 tcells 1- invert and ;

: bits/tcell   ( -- u ) config cell-width @   config char-bits @ * ;
: bytes/tcell  ( -- u ) bits/tcell   3 rshift ;
: bytes/tchar  ( -- u ) config char-bits @   3 rshift ;
: tchar-mask   ( -- u-mask ) 1   config char-bits @ lshift   1- ;

\ Output configuration =======================================================
native definitions
DEFER tcforth-output
:noname ( -- c-addr u )
  pad S" forth-" cat   machine cat   S" .bin" cat
  pad tuck - ; IS tcforth-output

host definitions
