\ Defines several configuration parameters which control different parts of
\ the metacompiler, eg. whether code space overlaps data space, whether code
\ space is read-only, whether to include name space and input parsing code, etc.

REQUIRE vocabularies.ft
host definitions

begin-structure configuration
  \ Including or excluding parts of the code.
  field: keyboard-input?
  field: keyboard-output?

  \ Nature of the target system:
  \ - If it includes the dictionary and Forth text interpreters
  \ - If the code is on a ROM and the data must be elsewhere.
  field: has-dictionary?
  field: names-indirect?
  field: data-indirect?

  \ Parameters of the target.
  field: char-bits
  field: cell-width      \ In target address units
  field: instr-width
  field: /codeword       \ In target address units
  field: alignment       \ Alignment of a target cell, in target address units.
  field: little-endian?
  field: threaded?       \ True for threaded code, ie. DTC and ITC.
end-structure

configuration allocate drop CONSTANT config

: interactive-forth! ( -- )
  config has-dictionary?  ON
  config keyboard-input?  ON
  config keyboard-output? ON ;

: standalone! ( -- )
  config has-dictionary?  OFF
  config names-indirect?  ON ;

: in-rom! ( -- )
  config data-indirect?   ON ;


\ Helpers ====================================================================
\ Typically 8 or 16, the number of bits per target address unit.
: tcharbits ( -- u )     config char-bits @ ;
: tcells    ( u1 -- u2 ) config cell-width @ * ;
: tcellbits ( -- u )     tcharbits tcells ;
: tchars    ( u1 -- u2 ) ;
: tchar+    ( u1 -- u2 ) 1+ ;
: tcell+    ( u1 -- u2 ) 1 tcells + ;

: taligned  ( tc-addr -- t-addr )
  config alignment @ 1-   tuck +   swap invert and ;

: bits/tcell   ( -- u ) config cell-width @   config char-bits @ * ;
: bytes/tcell  ( -- u ) bits/tcell   3 rshift ;
: bytes/tchar  ( -- u ) config char-bits @   3 rshift ;
: tchar-mask   ( -- u-mask ) 1   config char-bits @ lshift   1- ;

\ Output configuration =======================================================
native definitions
DEFER tcforth-output
:noname ( -- c-addr u )
  pad S" forth-" cat   machine cat   S" .bin" cat
  pad tuck - ; IS tcforth-output

host definitions
