\ Target words to get metacompiled, that hide the details of the machine and the
\ Forth model (eg. ITC vs. DTC vs. STC)
\ This file: DTC on the Mocha 86k

\ ,DOCOL is already defined.
target definitions

\ Add ,CF, which expects the address to jump to on the stack.
\ A code field here is jsrl lit_ul, $7c20 + the literal.
\ L000 oooo ooss ssss   o=5 s=3b, so $817b plus the 32-bit address.
target : ,CF ( codeword-addr -- ) $817b c, , ;

\ jsrl lit_uw, docol
\ L000 oooo ooss ssss   o=5 s=3a
target : ,DOCOL ( -- )
  [ host $817a tliteral target ] c,
  [ host docol tliteral target ] c, ;

target : LITERAL ( C: x --    X: -- x ) [ host lit tliteral target ] compile, , ; IMMEDIATE
  host acts: tliteral ; IMPERATIVE

\ Compiles a string literal into a thread.
\ On ITC/DTC that means the address of DOSTRING, the length, then the words.
target : ,DOSTRING ( c-addr u -- )
  [ host dostring tliteral target ] compile,   \ Compile the codeword first.
  dup >R c,        \ Then the length
  here R@ move     \ And the text
  R> chars allot ; \ And make room for it.

target : ALIGNED ;
target : ALIGN ;

\ This is actually portable but since it fits right here between ALIGNED and
\ (DOES>), there's no good place to put it with splitting this file into 3.
target : >CFA ( nt -- xt )
  cell+ dup c@ [ host mask_len tliteral target ] and + char+ aligned ;

target : PAD ( -- addr ) $fb00 ;

\ DOES> ======================================================================
\ Implementing sequence 2
\ (DOES>) runs in eg. CONSTANT to properly compile a particular constant.
\ Inside target (DOES>), TORS points at the EXIT. Just after it is the
\ dodoes-code that should become the new codeword's jump location.
\ $817a is JSRL lit_ul on DCPU.
target : (DOES>) latest >cfa   $817b over c!    char+ R@ cell+ swap ! ;

host T' (DOES>) '(DOES>) !
host T' EXIT    'EXIT    !

\ Runs during compilation TARGET : DOES> - compile code into its thread!
host definitions
: TDOES,
  [T'] (DOES>) tliteral  [T'] compile, tcompile,
  [T'] EXIT    tliteral  [T'] compile, tcompile,
  $817b        tliteral  [T'] c,       tcompile,
  docol        tliteral  [T'] c,       tcompile, ;


\ Debugging helpers ==========================================================
host definitions
: codeword+ ( xt -- tt ) 2 + ; \ JSR lit_uw + the literal
: ?tsee-lit    ( tt -- tt 0 | tt' -1 )
  dup t@ lit = IF
    tcell+ dup t@ dup . hex.   tcell+ true
  ELSE false THEN ;

: ?tsee-branch ( tt -- tt 0 | tt' val -1 )
  dup t@ dup [T'] (0branch) =   swap [T'] (branch) = or IF
    dup t@ T>NAME TID. bl emit
    tcell+ dup t@ hex. tcell+ true ELSE false THEN ;

: tsee-end? ( tt -- ? )   t@ [T'] EXIT = ;
: tsee-word ( tt -- tt' ) dup t@ t>name tid. tcell+ ;
