\ Error message strings for the various errors.
target definitions

CREATE (abortmsg) 0 , 0 ,

: ABORT"
    postpone IF
      postpone S"   ['] (abortmsg) compile,   ['] 2! compile,
      -2 postpone literal   ['] throw compile,
    postpone THEN ; IMMEDIATE
  \ host acts: postpone IF
  \     [T'] S" tcompile,   [T'] (abortmsg) tcompile,
  \     [T'] 2! tcompile,   -2 tliteral   [T'] throw tcompile,
  \   postpone THEN ; IMPERATIVE

target : .ERROR ( exc# -- )
  CASE
  -2                 OF ." *** " (abortmsg) 2@ type ENDOF
  err-undefined      OF ." Undefined word: " last-parse 2@ type ENDOF
  err-?comp          OF ." Comp word outside colon defn" ENDOF
  err-unexpected-EOF OF ." Disk unexpectedly ended or ejected" ENDOF
  ( exc# ) ." Exception " .
  0 ENDCASE ;

\ Extra debugging utilities and things.

\ Prints the name of a word given its nt (name token).
: id. ( nt -- )
  cell+
  dup char+ swap
  c@ [ host mask_len tliteral target ] and ( c-addr u )
  type space ;

\ Prints the names of all words, from LATEST back to the end.
: words ( -- ) latest BEGIN ?dup WHILE ( nt ) dup id. cr @ REPEAT ;
