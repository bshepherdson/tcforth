\ Last file for the topmost words like QUIT
target definitions

\ TODO Add DEFER, then use it for things like this refill-xt.
target VARIABLE refill-xt
host T' refill   T' refill-xt t>body t! target

target : \   source >in ! drop ; IMMEDIATE
  host acts: native postpone \ host ; IMPERATIVE-BOTH

target : (   41 parse 2drop ; IMMEDIATE
  host acts: native 41 parse 2drop host ; IMPERATIVE-BOTH

\ Fundamental multi-line interpreter loop.
\ Separate from QUIT because it gets called by file readers as well.
target : (QUIT) ( -- )
  BEGIN refill-xt @ 11 break execute 12 break WHILE
    [ host T' INTERPRET tliteral target ] catch
    CASE
    0   OF state @ 0=   source-id 0= and IF ."  ok" cr THEN ENDOF
    -1  OF aborted ENDOF
    ( exc# ) .ERROR cr aborted \ Print error message, clear stacks, and continue
    0 ENDCASE
  REPEAT ( TODO: BYE ) ;

\ Empties the return stack, selects keyboard input, forces interpreter state,
\ and (QUIT)s to keep interpreting.
target : QUIT ( -- )
  [ host rp0 tliteral target ] rp!
  [ host T' [ tcompile, target ]
  10 base !
  0  'source-id !
  (QUIT) ;

: .intro ( -- ) white fg! light blue bg!
  \  01234567890123456789012345678901
  ."        TC-FORTH version 7       "
  default-colours cr ;

\ Initiates a cold start - locate and init the hardware, then QUIT.
: cold ( -- )
  init-devices init-lem init-serial
  .intro QUIT ;

host   T' COLD   entry-point t!

