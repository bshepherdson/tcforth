\ Last file for the topmost words like QUIT
target definitions

target : \   source >in ! drop ; IMMEDIATE
  host acts: native postpone \ host ; IMPERATIVE-BOTH

target : (   41 parse 2drop ; IMMEDIATE
  host acts: native 41 parse 2drop host ; IMPERATIVE-BOTH

\ Fundamental multi-line interpreter loop.
\ Separate from QUIT because it gets called by file readers as well.
target : (QUIT) ( -- )
  BEGIN refill WHILE
    [ host T' INTERPRET tliteral target ] catch
    CASE
    0   OF state @ 0=   source-id 0= and IF ."  ok" cr THEN ENDOF
    -1  OF aborted ENDOF
    ( exc# ) .ERROR cr aborted \ Print error message, clear stacks, and continue
    0 ENDCASE
  REPEAT ;

\ Empties the return stack, selects keyboard input, forces interpreter state,
\ and (QUIT)s to keep interpreting.
target : QUIT ( -- )
  [ host rp0 tliteral target ] rp!
  [ host T' [ tcompile, target ]
  10 base !
  src-keyboard 'source-id !
  (QUIT) ( TODO: BYE ) ;


target : INIT-DATA-PTRS ( -- )
  [ host latest0 tliteral target ] @ [ host var_latest tliteral target ] !
  [ host dp0     tliteral target ] @ [ host var_dp     tliteral target ] ! ;


host config data-indirect? @ [IF]
\ Copy initialized data ======================================================
host definitions
code-space @   0 tcode,   CONSTANT initializers

S" Initializers at: " type initializers hex. cr

host
: initializer, ( space -- )
  code-space @   initializers tcode@
  tcode,   initializers tcode!
  dup space>target-span ( space t-addr u-tchars )
  over tcode,   dup tcode,   0 ?DO   ( space t-addr )
    2dup i + swap <tc@> ( space t-addr tchar )
    tcode-c, LOOP 2drop ;

: prepare-data-initializers ( -- )
  config data-indirect? @ 0= ABORT" Copying data to RAM without data-indirect? flag is pointless"

  all-spaces @ BEGIN ?dup WHILE
    dup space>init-copy? @ IF dup initializer, THEN
    space>next @
  REPEAT ;

\ The initializers form a linked list of: [ next | dst | len | tchars... ]
target definitions
: init-data ( -- )
  [ host initializers tliteral target ] @ BEGIN ?dup WHILE
    dup 3 cells +   over dup cell+ @   swap 2 cells + @ move
    @
  REPEAT
  \ This has to go after the above, so the correct values are written in.
  init-data-ptrs ;

host [ELSE]
\ Nothing to copy ============================================================
\ With an initialized data space, there's nothing to do here.
target definitions
: init-data ( -- ) init-data-ptrs ;

host [THEN] target definitions

\ Basic startup
: boot  ( -- ) init-data   init-hardware   10 base ! ;
: intro ( -- ) '.intro @ execute ;
\ "Cold" start, loading interactive mode.
: cold  ( -- ) boot intro QUIT ;

host   T' COLD   entry-point tcode!
