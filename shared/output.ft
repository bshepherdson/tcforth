target definitions

\ Basic output helpers =======================================================
: SPACE  ( -- )   bl emit ;
: SPACES ( u -- ) dup 0> IF 0 DO space LOOP ELSE drop THEN ;

: TYPE   ( c-addr u -- ) ?dup IF 0 DO ( c-addr ) dup c@ emit 1+ LOOP THEN drop ;

\ Pictured numeric ouput =====================================================
VARIABLE (picout)
: (picout-top) here 64 chars + ;
: <#   ( -- )    (picout-top) (picout) ! ;
: HOLD ( c -- )  (picout) @ 1- dup >R c!   R> (picout) ! ;
: SIGN ( n -- )  0< IF [ host '-' tliteral target ] hold THEN ;

: #    ( ud1 -- ud2 )
  dup 0 base @ um/mod >R drop ( ud1   R: hi-q )
  base @ um/mod               ( r lo-q   R: hi-q )
  swap dup 10 < IF
    [ host '0' tliteral target ]
  ELSE
    10 - [ host 'A' tliteral target ]
  THEN
  + HOLD ( lo-q   R: hi-q )
  R> ;

: #S   ( ud1 -- ud2 )
  2dup or 0= IF [ host '0' tliteral target ] hold EXIT THEN \ Special case 0
  BEGIN 2dup or WHILE # REPEAT ;

: #>   ( ud -- c-addr u ) 2drop   (picout) @ (picout-top) over - ;

: (#UHOLD) ( u -- c-addr u ) <# 0 #S #> ;
: U. ( u -- ) (#UHOLD) type space ;

: (#HOLD) ( n -- c-addr u )
  \ The smallest integer is a special case - it has no positive counterpart!
  [ host 1 tcellbits 1- lshift tliteral target ] over = IF
    <# 0 #S [ host '-' tliteral target ] hold #>
  ELSE <# dup abs S>D #S rot sign #> THEN ;

: .  ( n -- )       (#HOLD) type space ;
: .R ( n width -- ) >R (#HOLD) R> over - spaces type ;

: HEX. ( u -- )
  base @ >R hex
  [ host '$' tliteral target ] emit   U.
  R> base ! ;

\ Intro message ==============================================================
VARIABLE '.intro
:noname ." TC-Forth version 7" cr   hex latest .  dp . here . ; '.intro !
\ XXX: START HERE - latest is 0 and dp is the start of the data area, rather
\ than its end. Figure out where those two values are coming from and fix them!

\ Stack output ===============================================================
VARIABLE (.)
: (.S) ( xt -- )
  [char] < emit depth (#HOLD) type [char] > emit space
  depth ?dup IF 1- 0 swap DO i pick (.) @ execute -1 +LOOP THEN ;

: .S    ( -- ) ['] .    (.) ! (.S) ;
: HEX.S ( -- ) ['] hex. (.) ! (.S) ;

: ? ( a-addr -- ) @ . ;
